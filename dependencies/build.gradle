apply plugin: "com.github.gmazzo.buildconfig"

ext.detektVersion = "1.10.0"
buildConfig {
    packageName("net.meilcli.librarian.gradle.dependencies")
    className("Dependencies")
    buildConfigField("String", "detektVersion", "\"$detektVersion\"")
}

ext {
    configUrl = ""
    configName = ""
}

@SuppressWarnings("GrMethodMayBeStatic")
void repositories(Closure closure) {
    closure()
}

@SuppressWarnings("GrMethodMayBeStatic")
void maven(Closure closure) {
    closure()
    buildConfig.forClass("Repositories") {
        buildConfigField("String", configName, "\"$configUrl\"")
    }
    ext.set("repository_$configName", configUrl)
}

@SuppressWarnings("GrMethodMayBeStatic")
void url(String url) {
    ext.configUrl = url
}

@SuppressWarnings("GrMethodMayBeStatic")
void name(String name) {
    ext.configName = name
}

@SuppressWarnings("GrMethodMayBeStatic")
void dependencies(Closure closure) {
    closure()
}

@SuppressWarnings("GrMethodMayBeStatic")
void library(String notice, String name, String className) {
    buildConfig.forClass(className) {
        buildConfigField("String", name, "\"$notice\"")
    }
    ext.set("library_${className}_${name}", notice)
}

repositories {
    maven {
        url "https://maven.google.com/"
        name "google"
    }
    maven {
        url "https://jcenter.bintray.com/"
        name "jcenter"
    }
    maven {
        url "https://plugins.gradle.org/m2/"
        name "gradle"
    }
    maven {
        url "https://dl.bintray.com/meilcli/librarian"
        name "librarian"
    }
    maven {
        url "https://dl.bintray.com/mobile-act/RetektRule"
        name "retekt"
    }
}

dependencies {
    ext.kotlinGroup = "org.jetbrains.kotlin"
    ext.kotlinVersion = "1.3.72"
    library "$kotlinGroup:kotlin-gradle-plugin:$kotlinVersion", "gradle", "Kotlin"
    library "$kotlinGroup:kotlin-stdlib-jdk7:$kotlinVersion", "stdlib", "Kotlin"
    library "$kotlinGroup:kotlin-serialization:$kotlinVersion", "gradle", "KotlinSerialization"
    library "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0", "runtime", "KotlinSerialization"

    library "com.android.tools.build:gradle:4.0.1", "gradle", "Android"
    library "androidx.appcompat:appcompat:1.1.0", "appCompat", "Android"
    library "androidx.activity:activity:1.1.0", "activity", "Android"
    library "androidx.fragment:fragment:1.2.5", "fragment", "Android"
    library "androidx.recyclerview:recyclerview:1.1.0", "recyclerView", "Android"
    library "androidx.test.ext:junit:1.1.1", "junit", "Android"
    library "androidx.test.espresso:espresso-core:3.2.0", "espresso", "Android"

    ext.librarianGroup = "net.meilcli.librarian"
    ext.librarianVersion = "1.0.1"
    library "$librarianGroup:plugin-core:$librarianVersion", "pluginCore", "Librarian"
    library "$librarianGroup:plugin-preset:$librarianVersion", "pluginPreset", "Librarian"
    library "$librarianGroup:ui-core:$librarianVersion", "uiCore", "Librarian"
    library "$librarianGroup:ui-activity:$librarianVersion", "uiActivity", "Librarian"
    library "$librarianGroup:ui-serializer-kotlin:$librarianVersion", "uiSerializerKotlin", "Librarian"

    library "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5", "plugin", "Bintray"

    library "junit:junit:4.13", "junit", "Junit4"

    ext.junit5Group = "org.junit.jupiter"
    ext.junit5Version = "5.6.2"
    library "$junit5Group:junit-jupiter-api:$junit5Version", "api", "Junit5"
    library "$junit5Group:junit-jupiter-engine:$junit5Version", "engine", "Junit5"

    ext.detektGroup = "io.gitlab.arturbosch.detekt"
    library "$detektGroup:detekt-gradle-plugin:$detektVersion", "gradle", "Detekt"
    library "$detektGroup:detekt-formatting:$detektVersion", "formatting", "Detekt"

    library "mobile-act:retekt:0.1", "retekt", "MobileAct"

    ext.xmlUtilGroup = "net.devrieze"
    ext.xmlUtilVersion = "0.20.0.1"
    library "$xmlUtilGroup:xmlutil-jvm:$xmlUtilVersion", "core", "XmlUtil"
    library "$xmlUtilGroup:xmlutil-serialization-jvm:$xmlUtilVersion", "serialization", "XmlUtil"

    library "com.squareup.okio:okio:2.7.0", "okio", "SquareUp"
    library "com.squareup.moshi:moshi:1.12.0", "moshi", "SquareUp"
    library "com.squareup.moshi:moshi-kotlin-codegen:1.9.3", "moshiKapt", "SquareUp"
    library "com.squareup.okhttp3:okhttp:4.8.0", "okhttp", "SquareUp"
    library "com.squareup.retrofit2:retrofit:2.9.0", "retrofit", "SquareUp"

    library "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.5.0", "retrofitKotlinSerializationConverter", "JakeWharton"

    library "com.google.code.gson:gson:2.8.6", "gson", "Google"

    library "com.github.gmazzo:gradle-buildconfig-plugin:2.0.1", "gradleBuildConfigPlugin", "Gmazzo"
}