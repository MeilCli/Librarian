import net.meilcli.librarian.gradle.Dependencies

buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url "https://maven.pkg.github.com/MeilCli/Librarian"
            credentials {
                username System.getenv("GITHUB_USER")
                password System.getenv("GITHUB_TOKEN")
            }
        }
    }
    dependencies {
        if (local_build) {
            classpath fileTree(dir: 'libs', include: ['*.jar'])
            classpath Dependencies.Kotlin.Serialization.runtime
            classpath Dependencies.TikXml.annotation
            classpath Dependencies.TikXml.core
            classpath Dependencies.SquareUp.okio
        } else {
            classpath Dependencies.Librarian.pluginCore
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "net.meilcli.librarian"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation project(":sample-child")
    implementation project(':sample-child:sample-child-nested')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply plugin: 'librarian'

librarian {
    depth = "firstLevel"

    pages {
        implementation {
            title = "Sample production binary using libraries"
            description = "app modules' implementationDependenciesMetadata and releaseImplementationDependenciesMetadata list"
            configurations = [
                    "implementationDependenciesMetadata",
                    "releaseImplementationDependenciesMetadata"
            ]
        }
    }

    groups {
        "AndroidX" {
            artifacts = [
                    "androidx.appcompat:appcompat",
                    "androidx.core:core-ktx"
            ]
            author = "The Android Open Source Project"
            url = "https://developer.android.com/jetpack/androidx"
            licenseName = "The Apache Software License, Version 2.0"
            licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
}
