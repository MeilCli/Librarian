import net.meilcli.librarian.gradle.Dependencies

buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url "https://maven.pkg.github.com/MeilCli/Librarian"
            credentials {
                username System.getenv("USER_NAME")
                password System.getenv("GITHUB_TOKEN")
            }
        }
    }
    dependencies {
        if (local_build) {
            classpath fileTree(dir: 'libs', include: ['*.jar'])
            classpath Dependencies.Kotlin.Serialization.runtime
            classpath Dependencies.TikXml.annotation
            classpath Dependencies.TikXml.core
            classpath Dependencies.SquareUp.okio
        } else {
            classpath Dependencies.Librarian.pluginCore
        }
    }
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'
apply plugin: 'KotlinLibraryPlugin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'MavenPublishPlugin'

dependencies {
    implementation Dependencies.Kotlin.Serialization.runtime
    implementation Dependencies.TikXml.annotation
    implementation Dependencies.TikXml.core
    implementation Dependencies.SquareUp.okio
    kapt Dependencies.TikXml.processor
}

gradlePlugin {
    plugins {
        librarianPlugin {
            id = "librarian"
            displayName = "Librarian Plugin"
            description = "Librarian is generate notice that library used in gradle module"
            implementationClass = "net.meilcli.librarian.plugin.LibrarianPlugin"
        }
    }
}

apply plugin: 'librarian'

librarian {
    depth = "firstLevel"

    pages {
        "plugin-core-usings-plugin" {
            title = "Librarian plugin-core's using libraries"
            description = "This list is including plugin binary"
            configurations = [
                    "implementationDependenciesMetadata"
            ]
        }
        "plugin-core-usings-development" {
            title = "Librarian plugin-core's using libraries"
            description = "This list is using development of Librarian"
            configurations = [
                    "kapt",
                    "testImplementationDependenciesMetadata",
                    "testRuntimeOnlyDependenciesMetadata",
                    "classpath"
            ]
        }
    }

    groups {
        "Kotlin" {
            artifacts = [
                    "org.jetbrains.kotlin:kotlin-gradle-plugin",
                    "org.jetbrains.kotlin:kotlin-serialization",
                    "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
            ]
        }
        "TikXml" {
            artifacts = [
                    "com.tickaroo.tikxml:annotation",
                    "com.tickaroo.tikxml:core",
                    "com.tickaroo.tikxml:processor"
            ]
            author = "Tickaroo"
            url = "https://github.com/Tickaroo/tikxml"
            licenseName = "The Apache Software License, Version 2.0"
            licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
        "JUnit5" {
            artifacts = [
                    "org.junit.jupiter:junit-jupiter-api",
                    "org.junit.jupiter:junit-jupiter-engine"
            ]
            author = "JUnit Team"
        }
    }
}