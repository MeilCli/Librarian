import net.meilcli.librarian.gradle.Dependencies

apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'
apply plugin: 'KotlinLibraryPlugin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'MavenPublishPlugin'

dependencies {
    implementation Dependencies.Kotlin.Serialization.runtime
    implementation Dependencies.TikXml.core
    implementation Dependencies.TikXml.coreAnnotation
    implementation Dependencies.SquareUp.okio
    kapt Dependencies.TikXml.processor
}

gradlePlugin {
    plugins {
        librarianPlugin {
            id = "librarian"
            displayName = "Librarian Plugin"
            description = "Librarian is generate notice that library used in gradle module"
            implementationClass = "net.meilcli.librarian.plugin.LibrarianPlugin"
        }
    }
}

apply plugin: 'librarian'
apply plugin: 'librarian-preset'

librarian {
    depth = "firstLevel"

    pages {
        "plugin-core-usings-plugin" {
            title = "Librarian plugin-core's using libraries"
            description = "This list is including plugin binary"
            configurations = [
                    "implementationDependenciesMetadata"
            ]
        }
        "plugin-core-usings-development" {
            title = "Librarian plugin-core's using libraries"
            description = "This list is using development of Librarian"
            configurations = [
                    "kapt",
                    "testImplementationDependenciesMetadata",
                    "testRuntimeOnlyDependenciesMetadata",
                    "classpath"
            ]
        }
    }

    groups {
        "TikXml" {
            artifacts = [
                    "com.tickaroo.tikxml:annotation",
                    "com.tickaroo.tikxml:core",
                    "com.tickaroo.tikxml:processor"
            ]
            author = "Tickaroo"
            url = "https://github.com/Tickaroo/tikxml"
            licenseName = "The Apache Software License, Version 2.0"
            licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
        "JUnit5" {
            artifacts = [
                    "org.junit.jupiter:junit-jupiter-api",
                    "org.junit.jupiter:junit-jupiter-engine"
            ]
            author = "JUnit Team"
        }
        "gradle-bintray-plugin" {
            artifacts = [
                    "com.jfrog.bintray.gradle:gradle-bintray-plugin"
            ]
            url = "https://github.com/bintray/gradle-bintray-plugin"
        }
        "Librarian" {
            artifacts = [
                    "net.meilcli.librarian:plugin-core",
                    "net.meilcli.librarian:plugin-preset"
            ]
        }
    }
}